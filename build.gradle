plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
}

def majorMCVersion = Integer.parseInt(project.minecraft_version.split("\\.")[1])
sourceCompatibility = majorMCVersion > 17 ? JavaVersion.VERSION_17 : majorMCVersion > 16 ? JavaVersion.VERSION_16 : JavaVersion.VERSION_1_8
targetCompatibility = sourceCompatibility

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		url = "https://jitpack.io"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "com.github.RedLime:yarn:$project.yarn_mappings:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// MixinExtras
	include(api(annotationProcessor "com.github.llamalad7.mixinextras:mixinextras-fabric:${project.mixinextras_version}"))

	// Fabric API
	include(modApi fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version))
	include(modApi fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
		expand "minecraft_version": project.minecraft_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	it.options.release = majorMCVersion > 17 ? 17 : majorMCVersion > 16 ? 16 : 8
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	// withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/octocat/hello-world"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}